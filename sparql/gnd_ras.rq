# Mapping gnd ./. repec author service
#
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX schema: <http://schema.org/>
#
select distinct ?gnd ?gndLabel ?gndId ?ras ?rasLabel ?rasId (group_concat(distinct ?affiliation ; separator = "; ") as ?affiliations)
where {
  graph <http://zbw.eu/beta/gndRas/ng> {
    ?ras skos:exactMatch ?gnd .
  }
  graph <http://zbw.eu/beta/repec/ng> {
    ?ras foaf:name ?rasLabel .
    optional {
      ?ras schema:affiliation ?affiliation .
    }
  }
  # make this optional, if the gnd graph is not loaded
  optional {
    graph <http://zbw.eu/beta/gnd/ng> {
      ?gnd gndo:preferredNameForThePerson ?gndLabel .
    }
  }
  bind(strafter(str($gnd), '/gnd/') as ?gndId)
  bind(strafter(str($ras), '/pro/') as ?rasId)
}
group by ?gnd ?gndLabel ?gndId ?ras ?rasLabel ?rasId


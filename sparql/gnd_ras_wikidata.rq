# Mapping GND ./. Repec Author Service via Wikidata items linked to GND and RAS
#
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#
select distinct ?gnd ?gndLabel ?ras ?rasLabel ?wd (str(?wdLabelEn) as ?wdLabel) ?orcid
where {
  # get all wikidata entries for humans with gnd
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    ?wd wdt:P2428 ?rasId .
    ?wd wdt:P227 ?gndId ;
        wdt:P31 wd:Q5 .
    optional {
      ?wd rdfs:label ?wdLabelEn .
      filter(lang(?wdLabelEn) = 'en')
    }
    optional {
      ?wd wdt:P496 ?orcidId
    }
  }
  bind(uri(concat("http://authors.repec.org/pro/", $rasId)) as ?ras)
  bind(uri(concat("http://d-nb.info/gnd/", $gndId)) as ?gnd)
  bind(uri(concat("http://orcid.org/", ?orcidId)) as ?orcid)
  #
  # make this optional, if the gnd graph is not loaded
  optional {
    graph <http://zbw.eu/beta/gnd/ng> {
      ?gnd gndo:preferredNameForThePerson ?gndLabel .
    }
  }
  optional {
    graph <http://zbw.eu/beta/repec/ng> {
      ?ras foaf:name ?rasLabel .
    }
  }
}
order by asc(?gndLabel)

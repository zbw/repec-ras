# Numbers for the GND/RAS mapping via Wikidata as well as from 
# ZBW's external mapping
#
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#
# display author pairs
#select *
# display summary
select (count(distinct ?gnd) as ?mappingCount) (sum(?inWikidata) as ?inWdCount) (sum(?inZbwMapping) as ?inZbwMappingCount) (sum(?inBoth) as ?inBothCount) ((?inWdCount - ?inBothCount) as ?onlyInWikidataCount)
where {
  #select *
  select ?gnd (max(?gndLabel1) as ?gndLabel) ?ras (max(?rasLabel1) as ?rasLabel) (max(?inZbwMapping1) as ?inZbwMapping) (max(?inBoth1) as ?inBoth) (max(?inWikidata1) as ?inWikidata) (max(?wdLabel) as ?wikidataLabel)
  where {
    {
      # get all wikidata entries for humans with gnd
      service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
        ?wd wdt:P2428 ?rasId ;
            wdt:P227 ?gndId ;
            wdt:P31 wd:Q5 .
        filter(isLiteral(?gndId) && isLiteral(?rasId))
        optional {
          ?wd rdfs:label ?wdLabelLang .
          filter(lang(?wdLabelLang) = 'en')
        }
      }
      bind(str(?wdLabelLang) as ?wdLabel)
      bind(uri(concat("http://authors.repec.org/pro/", $rasId)) as ?ras)
      bind(uri(concat("http://d-nb.info/gnd/", $gndId)) as ?gnd)
    } union {
      graph <http://zbw.eu/beta/gndRas2/ng> {
        ?ras skos:exactMatch ?gnd .
      }
    }
    bind(if(exists {
          graph <http://zbw.eu/beta/gndRas2/ng> {
            ?ras skos:exactMatch ?gnd .
          }
        }, 1, 0) as ?inZbwMapping1)
    bind(if(bound(?wd), 1 , 0) as ?inWikidata1)
    bind(if((?inWikidata1 = 1) && (?inZbwMapping1 = 1), 1, 0) as ?inBoth1)
    #
    graph <http://zbw.eu/beta/gnd/ng> {
      ?gnd gndo:preferredNameForThePerson ?gndLabel1 .
    }
    graph <http://zbw.eu/beta/repec/ng> {
      ?ras foaf:name ?rasLabel1 .
    }
  }
  group by ?gnd ?ras
  order by ?gndLabel
}

# Create input for item creation via Wikidata QuickStatements
#
PREFIX dbpo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX opus: <http://lsdis.cs.uga.edu/projects/semdis/opus#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?rasId ?rasName ?worksFor
(max(?gndId1) as ?gndId) (max(?gndName1) as ?gndName) (max(?gender1) as ?gender)
(max(?birthDate1) as ?birthDate) (max(?deathDate1) as ?deathDate)
(group_concat(str(?info1); separator = ' / ') as ?info)
where {
  {
    select ?ras
    where {
      graph <http://zbw.eu/beta/repec/ng> {
        ?ras a foaf:Person ;
             zbwext:repecCount ?count .
        filter(?count > 0)
      }
    }
  }
  # filter out the already existing ras entries
  minus
  {
    select ?ras
    where {
      # all items with a repec short-id property
      service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
        ?wd wdt:P2428 ?rasId .
        # filter out blank nodes ("unknown value")
        filter(isLiteral(?rasId))
      }
      bind(uri(concat("http://authors.repec.org/pro/", ?rasId)) as ?ras)
    }
  }
  # TODO filter out the already existing gnd entries
  #
  # restrict to the external mapping
  graph <http://zbw.eu/beta/gndRas2/ng> {
    ?ras skos:exactMatch ?gnd .
  }
  bind(strafter(str($gnd), '/gnd/') as ?gndId1)
  bind(strafter(str($ras), '/pro/') as ?rasId)
  #
  # supplement with repec data
  graph <http://zbw.eu/beta/repec/ng> {
    ?ras foaf:name ?rasName .
    optional {
      ?ras schema:worksFor ?worksFor .
    }
  }
  # supplement with gnd data - info (and perhaps birth/death) occur multiple times!
  #  service <http://zbw.eu/beta/sparql/gnd/query> {
  graph <http://zbw.eu/beta/ebds/gnd/ng> {
    ?gnd gndo:preferredNameForThePerson ?gndName1.
    optional {
      ?gnd gndo:dateOfBirth ?birthDate1.
    }
    optional {
      ?gnd gndo:dateOfDeath ?deathDate1.
    }
    optional {
      ?gnd gndo:biographicalOrHistoricalInformation ?info1
    }
    optional {
      ?gnd gndo:gender ?genderResource .
      bind(strafter(str(?genderResource), 'http://d-nb.info/standards/vocab/gnd/gender#') as ?gender1)
    }
  }
}
group by ?rasId ?rasName ?worksFor

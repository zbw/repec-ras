# Create input for item creation via Wikidata QuickStatements
#
PREFIX dbpo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX opus: <http://lsdis.cs.uga.edu/projects/semdis/opus#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?rank ?id ?name ?description ?gndName ?birthData ?deathDate ?info
where {
  values (?rankGraph) {
    #(<http://zbw.eu/beta/rasRankTop10pctFemale-february2017/ng>)
    (<http://zbw.eu/beta/rasRankTop10pct-february2017/ng>)
  }
  {
    select ?ras
    where {
      graph <http://zbw.eu/beta/repec/ng> {
        ?ras a foaf:Person ;
             zbwext:repecCount ?count .
        filter(?count > 0)
      }
    }
  }
  # filter out the already existing entries
  minus
  {
    select ?ras
    where {
      # all items with a repec short-id property
      service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
        ?wd wdt:P2428 ?rasId .
        # filter out blank nodes ("unknown value")
        filter(isLiteral(?rasId))
      }
      bind(uri(concat("http://authors.repec.org/pro/", ?rasId)) as ?ras)
    }
  }
  # restrict to ranking
  graph ?rankGraph {
    ?ras dbpo:rank ?rank .
  }
  bind(xsd:integer(?rank) as ?rankNumeric)
  filter(?rankNumeric < 150)
  #
  # restrict to mapping
  graph <http://zbw.eu/beta/gndRas2/ng> {
    ?ras skos:exactMatch ?gnd .
  }
  # supplement with repec data
  graph <http://zbw.eu/beta/repec/ng> {
    ?ras foaf:name ?rasName .
    #    optional {
    #      ?ras zbwext:repecCount ?pubCount .
    #    }
  }
  # supplement with gnd data
  service <http://zbw.eu/beta/sparql/gnd/query> {
    ?gnd gndo:preferredNameForThePerson ?gndName .
    optional {
      ?gnd gndo:dateOfBirth ?birthDate .
    }
    optional {
      ?gnd gndo:dateOfDeath ?deathDate .
    }
    optional {
      ?gnd gndo:biographicalOrHistoricalInformation ?info
    }
  }
  bind(strafter(str(?ras), 'http://authors.repec.org/pro/') as ?id)
  bind(concat('economist (', ?affiliations, ')') as ?description)
  bind(?rasName as ?name)
}
order by asc(?rankNumeric)

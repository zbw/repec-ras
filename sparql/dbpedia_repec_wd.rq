# Connect from dbpedia to wikidata in order to relate
# repec ids to wikidata items
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX schema: <http://schema.org/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
#
select distinct ?dbpedia ?rasId ?ras ?rasLabel ?wd ?wdLabel ?wdId ?rasIdInWd
where {
  graph <http://zbw.eu/beta/dbpedia/ng> {
    ?dbpedia dbp:repecId ?repecId ;
             owl:sameAs ?wd .
    filter(contains(str(?wd), 'wikidata.org/entity/'))
    bind(strafter(str($wd), '/entity/') as ?wdId)
    filter(?wdId not in (
        # filter out wrong duplicate mappings
        "Q39803",
        "Q5637845",
        "Q16187225",
        "Q4516788",
        "Q7358597",
        "Q4731030",
        "Q4799511",
        "Q4802507",
        "Q16143487",
        # other faulty assignments
        "Q21229533",
        "Q7791081"
      ))
    # remove language tag
    bind(str(?repecId) as ?rasId)
    bind(uri(concat('http://authors.repec.org/pro/', ?rasId)) as ?ras)
  }
  #
  graph <http://zbw.eu/beta/repec/ng> {
    ?ras foaf:name ?rasLabel .
  }
  service  <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    ?wd rdfs:label ?pref .
    optional {
      ?wd wdt:P2428 ?rasIdInWd .
    }
    filter(lang(?pref) = 'en')
    bind(str(?pref) as ?wdLabel)
  }
}


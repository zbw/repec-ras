# Connect from dbpedia to wikidata in order to relate
# repec ids to wikidata items
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX schema: <http://schema.org/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
#
select distinct ?dbpedia ?rasId ?ras ?rasLabel ?wd ?wdLabel ?wdId # (group_concat(distinct ?affiliation ; separator = "; ") as ?affiliations)
where {
  graph <http://zbw.eu/beta/ebds/dbpedia/ng> {
    ?dbpedia dbp:repecId ?repecId ;
             owl:sameAs ?wd .
    filter(contains(str(?wd), 'wikidata.org/entity/'))
    bind(strafter(str($wd), '/entity/') as ?wdId)
    filter(?wdId not in (
        # filter out wrong duplicate mappings
        "Q39803",
        "Q5637845",
        "Q16187225",
        "Q4516788",
        "Q7358597",
        "Q4731030",
        "Q4799511",
        "Q4802507",
        "Q16143487",
        # other faulty assignments
        "Q21229533",
        "Q7791081"
      ))
    # remove language tag
    bind(str(?repecId) as ?rasId)
    bind(uri(concat('http://authors.repec.org/pro/', ?rasId)) as ?ras)
  }
  #
  graph <http://zbw.eu/beta/ebds/repec/ng> {
    ?ras foaf:name ?rasLabel ;
         schema:affiliation ?affiliation .
  }
  service  <http://172.16.10.102:3030/wikidata/query> {
    ?wd skos:prefLabel ?pref .
    filter(lang(?pref) = 'en')
    bind(str(?pref) as ?wdLabel)
  }
}
group by ?dbpedia ?ras ?rasId ?rasLabel ?wd ?wdLabel ?wdId

# Create input for Wikidata Mix'n'match for top economists
#
PREFIX dbpo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX opus: <http://lsdis.cs.uga.edu/projects/semdis/opus#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?id ?name ?description
where {
  values (?rankGraph) {
    ## (<http://zbw.eu/beta/rasRankTop10pctFemale-february2017/ng>) 
    (<http://zbw.eu/beta/rasRankTop10pct-february2017/ng>) 
  }
  {
    select ?ras (group_concat(distinct ?affiliation ;
        separator = "; ") as ?affiliations)
    where {
      graph <http://zbw.eu/beta/repec/ng> {
        #        bind(<http://authors.repec.org/pro/pdu166> as ?ras)
        ?ras opus:affiliation/foaf:name ?affiliation .
      }
    }
    group by ?ras
  }
  graph ?rankGraph {
    ?ras dbpo:rank ?rank .
  }
  graph <http://zbw.eu/beta/repec/ng> {
    ?ras foaf:name ?name .
    optional {
      ?ras zbwext:repecCount ?pubCount .
    }
  }
  bind(strafter(str(?ras), 'http://authors.repec.org/pro/') as ?id)
  bind(xsd:integer(?rank) as ?rankNumeric)
  bind(concat(?affiliations, ' (rank: ', ?rank, ', publications: ', str(?pubCount), ')') as ?description)
}
order by asc(?rankNumeric)

# Count the gnd-ras mappings and how many of them are in the RePEc/IDEAS
# lists of top 10% and top 10% female economists, and in the top 100 or 
# in the top 1000 derived from the former. Furthermore, the according gnds
# present in Wikidata are counted.
#
# see https://ideas.repec.org/top/ for background on the ranking
#
PREFIX dbpo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#
select (count(distinct ?ras) as ?gndRasCount) (count(distinct(?rankAll)) as ?rankAllCount)
(concat(str(round(count(distinct(?rankAll))/max(?allTotal)*1000)/10), ' %') as ?allPercentage)
(count(distinct(?rankFemale)) as ?rankFemaleCount) 
(concat(str(round(count(distinct(?rankFemale))/max(?femaleTotal)*1000)/10), ' %') as ?femalePercentage)
(count(?top100) as ?top100Count)
(count(?top1000) as ?top1000Count)
(sum(?inWd) as ?inWdCount)
(sum(?femaleInWd) as ?femaleInWdCount)
(sum(?top100inWd) as ?top100inWdCount)
(sum(?top1000inWd) as ?top1000inWdCount)
where {
  graph <http://zbw.eu/beta/gndRas/ng> {
    ?ras skos:exactMatch ?gnd .
  }
  optional {
    graph <http://zbw.eu/beta/rasRankTop10pct-february2017/ng> {
      ?ras dbpo:rank ?rankAll .
    }
  }
  optional {
    graph <http://zbw.eu/beta/rasRankTop10pctFemale-february2017/ng> {
      ?ras dbpo:rank ?rankFemale .
    }
  }
  optional {
    graph <http://zbw.eu/beta/rasRankTop10pct-february2017/ng> {
      ?ras dbpo:rank ?top100 .
      filter(xsd:integer(?top100) <= 100)
    }
  }
  optional {
    graph <http://zbw.eu/beta/rasRankTop10pct-february2017/ng> {
      ?ras dbpo:rank ?top1000 .
      filter(xsd:integer(?top1000) <= 1000)
    }
  }
  # which of these authors are linked in Wikidata?
  bind(strafter(str($gnd), '/gnd/') as ?gndId)
  bind(strafter(str($ras), '/pro/') as ?rasId)
  optional {
    service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
      {
        ?wd wdt:P227 ?gndId .
      } union {
        ?wd wdt:2428 ?rasId .
      }
    }
  }
  bind(if(bound(?wd), 1, 0) as ?inWd)
  bind(if((bound(?wd) && bound(?rankFemale)), 1, 0) as ?femaleInWd)
  bind(if((bound(?wd) && bound(?top100)), 1, 0) as ?top100inWd)
  bind(if((bound(?wd) && bound(?top1000)), 1, 0) as ?top1000inWd)
  #
  # compute total values for percentages
  {
    select (count(*) as ?allTotal)
    where {
      graph <http://zbw.eu/beta/rasRankTop10pct-february2017/ng> {
        ?x dbpo:rank [] .
      }
    }
  } 
  {
    select (count(*) as ?femaleTotal)
    where {
      graph <http://zbw.eu/beta/rasRankTop10pctFemale-february2017/ng> {
        ?x dbpo:rank [] .
      }
    }
  } 
}

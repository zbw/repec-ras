# Identify missing properties which can be provided by a
# mapping (example: GND ID / RePEc Short-ID)
#
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
#
select ?wdId ?defined ?definedLabel ?definedId ?missing ?missingLabel ?missingId
where {
  values ( ?mappingGraph ?definedGraph ?definedLblProp ?definedProperty ?definedStub ?missingGraph ?missingLblProp ?missingProperty ?missingStub ) {
    ( <http://zbw.eu/beta/gndRas2/ng> <http://zbw.eu/beta/repec/ng> foaf:name wdt:P2428 "http://authors.repec.org/pro/" <http://zbw.eu/beta/gnd/ng> gndo:preferredNameForThePerson wdt:P227 "http://d-nb.info/gnd/" ) 
  }
  # get all identified items with without ?missingProperty
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    ?wd ?definedProperty ?definedId .
    filter(not exists {
        ?wd ?missingProperty [] .
      })
  }
  bind(uri(concat(?definedStub, $definedId)) as ?defined)
  #
  # restrict to the items covered by the mapping
  # (both directions)
  graph ?mappingGraph {
    {
      ?defined skos:exactMatch ?missing .
    } union {
      ?missing skos:exactMatch ?defined .
    }
  }
  bind(strafter(str($missing), ?missingStub) as ?missingId)
  bind(strafter(str($wd), '/entity/') as ?wdId)
  #
  # add labels (optionally)
  optional {
    graph ?definedGraph {
      ?defined ?definedLblProp ?definedLabel .
    }
  }
  optional {
    graph ?missingGraph {
      ?missing ?missingLblProp ?missingLabel .
    }
  }
}

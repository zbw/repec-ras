# Identify missing properties which can be provided by a
# mapping (example: GND ID / RePEc Short-ID)
#
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
#
select ?wdId ?source ?sourceLabel ?sourceId ?target ?targetLabel ?targetId
where {
  values ( ?mappingGraph ?sourceGraph ?sourceLblProp ?sourceWdProp ?sourceStub ?targetGraph ?targetLblProp ?targetWdProp ?targetStub ) {
    ( <http://zbw.eu/beta/gndRas2/ng> <http://zbw.eu/beta/repec/ng> foaf:name wdt:P2428 "http://authors.repec.org/pro/" <http://zbw.eu/beta/gnd/ng> gndo:preferredNameForThePerson wdt:P227 "http://d-nb.info/gnd/" ) 
  }
  # get all identified items with without ?targetWdProp
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    ?wd ?sourceWdProp ?sourceId .
    filter(not exists {
        ?wd ?targetWdProp [] .
      })
  }
  bind(uri(concat(?sourceStub, $sourceId)) as ?source)
  #
  # restrict to the items covered by the mapping
  # (both directions)
  graph ?mappingGraph {
    {
      ?source skos:exactMatch ?target .
    } union {
      ?target skos:exactMatch ?source .
    }
  }
  bind(strafter(str($target), ?targetStub) as ?targetId)
  bind(strafter(str($wd), '/entity/') as ?wdId)
  #
  # add labels (optionally)
  optional {
    graph ?sourceGraph {
      ?source ?sourceLblProp ?sourceLabel .
    }
  }
  optional {
    graph ?targetGraph {
      ?target ?targetLblProp ?targetLabel .
    }
  }
}

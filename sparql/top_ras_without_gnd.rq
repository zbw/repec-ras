# Top RePEc economists without GND ID 
# (via Wikidata - all these economists had been added to WD before)
#
PREFIX dbpo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX hint: <http://www.bigdata.com/queryHints#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#
select ?wd ?ras ?rasLabel ?rasId ?gndId ?rank
where {
  service <https://query.wikidata.org/sparql> {
    hint:Query hint:optimizer "None" .
    ?wd wdt:P2428 ?rasId .
    optional {
      ?wd wdt:P227 ?gndId .
    }
  }
  #graph <http://zbw.eu/beta/rasRankTop10pct-february2017/ng> {
  graph <http://zbw.eu/beta/rasRankTop10pctFemale-february2017/ng> {
    ?ras dbpo:rank ?rank .
    bind(strafter(str($ras), '/pro/') as ?rasId)
  }
  filter(!bound(?gndId))
  graph <http://zbw.eu/beta/repec/ng> {
    ?ras foaf:name ?rasLabel .
  }
}
order by xsd:integer(?rank)

